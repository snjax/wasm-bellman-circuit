plugins {
    id "java-library"
    id "maven-publish"
    id "signing"
    id "io.github.gradle-nexus.publish-plugin" version "1.3.0"
}

repositories {
    mavenCentral()
}

def pr() { project.findProperty('pr').toString() ?: "" }

def ref() { project.findProperty('ref').toString().replaceAll('^.+/(.+)$', '$1') ?: "" }

ext {
    baseVersion = "0.2.0"

    publishingType = {
        if (pr()?.isNumber()) "snapshot" else if (ref() == "v${baseVersion}") "staging" else "prohibited"
    }

    artifactVersion = {
        if (pr()?.isNumber()) "${baseVersion}-${pr()}-SNAPSHOT" else baseVersion
    }
}

group = "com.wavesplatform"
archivesBaseName = "zwaves"
version = artifactVersion()

dependencies {
    testImplementation 'junit:junit:4.12'
}

test {
    useJUnit()
    maxHeapSize = '1G'
}

tasks.register('printVersion') {
    doLast {
        println version
    }
}

tasks.register('publishingType') {
    doLast {
        println publishingType()
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                packaging = 'jar'
                url = 'https://github.com/wavesplatform/zwaves'
                licenses {
                    license {
                        name.set("MIT License")
                        url.set("LICENSE")
                    }
                }
                scm {
                    connection.set("scm:git:git://github.com/wavesplatform/zwaves.git")
                    developerConnection.set("scm:git:git@github.com:wavesplatform/zwaves.git")
                    url.set("https://github.com/wavesplatform/zwaves")
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(gpgKey, gpgPassphrase)
    sign publishing.publications.mavenJava
}

nexusPublishing {
    repositories {
        sonatype()
    }
}
